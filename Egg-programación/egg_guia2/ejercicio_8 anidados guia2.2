
//Bucle "Para"
// 8. Un docente de Programación tiene un listado de 3 notas registradas por cada uno de sus
// N estudiantes. La nota final se compone de un trabajo práctico Integrador (35%), una
// Exposición (25%) y un Parcial (40%). El docente requiere los siguientes informes claves de
// sus estudiantes:
//
/// - Nota promedio final de los estudiantes que reprobaron el curso. Un estudiante
//   reprueba el curso si tiene una nota final inferior a 6.5
/// - Porcentaje de alumnos que tienen una nota de integrador mayor a 7.5.
/// - La mayor nota obtenida en las exposiciones.
/// - Total de estudiantes que obtuvieron en el Parcial entre 4.0 y 7.5.
//
// El programa pedirá la cantidad de alumnos que tiene el docente y en cada alumno pedirá
// las 3 notas y calculará todos informes claves que requiere el docente.
Algoritmo ejercicio_8
	Definir estudiantes, puntaje, integrador, exposicion, parcial Como real
	definir promedio_final, porce_integrador, mayor_expo, suma_reprobados Como Real
	Definir i , j, x, contador Como Entero
	Escribir "¿Cuántos alumnos estan en el curso?"
	leer estudiantes
	contador <- 0
	mayor_expo <- 0
	j <- 0
	x <- 0
	suma_reprobados <- 0
	Para i <- 1 Hasta estudiantes 
		
		Escribir "- Estudiante ", i, " -"
		Escribir sin saltar "Nota del trabajo práctico Integrador:"
		leer integrador
		Si integrador > 7.5 Entonces
			j <- j + 1
		Fin Si
		
		Escribir sin saltar "Nota de exposición:"
		leer exposicion
		Si exposicion > mayor_expo Entonces
			mayor_expo <- exposicion
		Fin Si
		
		Escribir sin saltar "Nota del parcial:"
		leer parcial
		Si parcial >= 4 y parcial <= 7.5 Entonces
			contador <- contador + 1
		Fin Si
		
		puntaje = (0.35 * integrador) + (0.25 * exposicion) + (0.4 * parcial)
		
		Si puntaje < 6.5 Entonces
			suma_reprobados <- puntaje + suma_reprobados
			x <- x + 1
		Fin Si
		
		escribir "SU PUNTAJE ES: ", puntaje
		Escribir ""
	Fin Para
	Si x <> 0 Entonces
		Escribir "Nota promedio final de los estudiantes que reprobaron el curso: ", (suma_reprobados/x)
	SiNo
		Escribir "No hubo reprobados"
	Fin Si
	
	Escribir "Porcentaje de alumnos que tienen una nota de integrador mayor a 7.5: ", (j / estudiantes)*100, "%"
	Escribir "La mayor nota obtenida en las exposiciones: ", mayor_expo
	Escribir "Total de estudiantes que obtuvieron en el Parcial entre 4.0 y 7.5: ", contador
FinAlgoritmo
	
	